@@ Dependencies: SceneSys

th u(NEWCOBJ,Vote System,Vote,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`VOTE [u(cobj,vote)]=$+vote */*: @check setr(1,pmatch(%1))=@nspemit/silent %#=Who do you want to vote for? ; @stop u(fun`is-alt,%:,setr(1,objid(%q1)))=@nspemit/silent %#=You can't vote for yourself. ; @stop haspower(%#,guest)=@nspemit/silent %#=Guests can't vote. ; @stop haspower(%q1,guest)=@nspemit/silent %#=You can't vote for guests. ; @check setr(0,u(fun`is-scene,%0))=@nspemit/silent %#=I can't find that scene ID. ; @check u(fun`poses-by,%:,%q0)=@nspemit/silent %#=You weren't in that scene. ; @check setr(p,u(fun`poses-by,%q1,%q0))=@nspemit/silent %#=[name(%q1)] wasn't in that scene. ; @check gte(u(fun`poses-in-scene,%q0),8)=@nspemit/silent %#=That scene's a bit too short to vote on. ; @check gte(%qp,3)=@nspemit/silent %#=name(%q1) hasn't said enough to vote for them. ; @check u(%fun`recent-scene,%q0)=@nspemit/silent %#=That scene is too old to vote for. ; @stop first(setr(v,u(fun`recent-votes,%:,%q1,%q0)),|)=@nspemit/silent %#=You already voted for [name(%q1)] in that scene. ; @check gte(sub(secs(),firstof(last(%qv,|),0)),86400)=@nspemit/silent %#=You can't vote for [name(%q1)] twice in one day. ; @nspemit/silent %#=You voted for [name(%q1)]. ; @nspemit/silent %q1=%n just voted for you! Nice work! ; @attach %!/inc`vote=%:,%q1,%q0

&CMD`VOTE-ALL [u(cobj,vote)]=$+vote/all *: @stop haspower(%#,guest)=@nspemit/silent %#=Guests can't vote. ; @check setr(0,u(fun`is-scene,%0))=@nspemit/silent %#=I can't find that scene ID. ; @check u(fun`poses-by,%:,%q0)=@nspemit/silent %#=You weren't in that scene. ; @check gte(u(fun`poses-in-scene,%q0),8)=@nspemit/silent %#=That scene's a bit too short to vote on. ; @check words(setr(1,u(fun`players-in-scene,%q0,%:)),|)=@nspemit/silent %#=Noone has posed in that scene yet. ; @check u(%fun`recent-scene,%q0)=@nspemit/silent %#=That scene is too old to vote for. ; think setq(a,u(fun`list-alts,%q0))[setq(1,filterbool(#lambda/not(member(%qa,first(%%0,^))),%q1,|))] ; think setq(previousVote,filterbool(#lambda/elements(%%0,3,^),%q1,|))[setq(1,setdiff(%q1,%q<previousVote>,|))][setq(tooFewPoses,filterbool(#lambda/lt(elements(%%0,2,^),3),%q1,|))][setq(1,setdiff(%q1,%q<tooFewPoses>,|))][setq(tooRecentVote,filterbool(#lambda/lte(sub([secs()],elements(%%0,4,^)),86400),%q1,|))][setq(1,setdiff(%q1,%q<tooRecentVote>,|))] ; @check words(%q1,|)=@attach %!/inc`bad-votees=There is noone in that scene you can vote for,%q<previousVote>,%q<tooFewPoses>,%q<tooRecentVote> ; @if words(squish(%q<previousVote>|%q<tooFewPoses>|%q<tooRecentVote>,|),|)={@attach %!/inc`bad-votees=There are some players in that scene you cannot vote for,%q<previousVote>,%q<tooFewPoses>,%q<tooRecentVote>} ; think setq(1,iter(%q1,first(%i0,^),|,%b)) ; @nspemit/silent %#=You vote for [itemize(iter(%q1,name(%i0),,|),|)] in scene %q0. ; @nspemit/silent/list %q1=%n just voted for you! Nice work! ; @attach %!/inc`vote=%:,%q1,%q0

&CMD`WVOTE [u(cobj,vote)]=$+wvote *: @check hasflag(%#,wizard)=@attach %!/inc`msg=ERROR: You aren't magical enough to do that. ; @attach %!/inc`checkpc=%0,1 ; @check isdbref(%q<t1acc>)=@attach %!/inc`msg=ERROR: [name(%q<t1>)] is not bound to an account. ; @stop u(fun`is-alt,%:,%q<t1>)=@attach %!/inc`msg=ERROR: You can't vote for yourself. ; @check lt(words(setr(2,graball(get(%#/d`wvotes),setr(1,timefmt($Y$m$d))))),3)=@attach %!/inc`msg=ERROR: You're out of wizvotes for today. ; @attach %!/inc`msg=You wvote for [name(%q<t1>)]. ; @attach %!/inc`msg=%n has granted you a WizVote. Congratulations!,%q<t1> ; &d`wvotes %#=squish(%q2 %q1) ; @attach %!/inc`ap=%:,%q0,2

&FUN`IS-ALT [u(cobj,vote)]=member(u(alts,%0),%1)
&FUN`IS-SCENE [u(cobj,vote)]=u(u(cobj,scene)/mysql,vote`is-scene,%0)
&FUN`LIST-ALTS [u(cobj,vote)]=
&FUN`PLAYERS-IN-SCENE [u(cobj,vote)]=u(u(cobj,scene)/mysql2,vote`players-in-scene,%0,%1,%0,%1,%0,%1)
&FUN`POSES-BY [u(cobj,vote)]=u(u(cobj,scene)/mysql,vote`poses-by,%1,%0)
&FUN`POSES-IN-SCENE [u(cobj,vote)]=u(u(cobj,scene)/mysql,vote`poses-in-scene,%0)
&FUN`RECENT-SCENE [u(cobj,vote)]=lte(u(u(cobj,scene)/mysql,vote`secs-since-start,%0),1209600)
&FUN`RECENT-VOTES [u(cobj,vote)]=sql({SELECT (SELECT COUNT(*) FROM `votes` WHERE `voter`='%0' AND `votee`='%1' AND `scene_id`='%2') AS `voted`, UNIX_TIMESTAMP(`vote_date`) AS `when` FROM votes WHERE `voter`='%0' AND `votee`='%1' ORDER BY `vote_date` DESC LIMIT 1},,|)

&INC`AP [u(cobj,vote)]=&d`ap %1=[add(default(%1/d`ap,0),%2)] ; &d`ap`total %1=[add(default(%1/d`ap`total,0),%2)]
&INC`BAD-VOTEES [u(cobj,vote)]=@nspemit/silent %#=%0: [edit(squish(if(%1,You have already voted for [itemize(iter(%1,name(first(%i0,^)),|,|),|)].)|[if(%2,itemize(iter(%2,name(first(%i0,^)),|,|),|) [switch(words(%2,|),1,has,have)] not posed enough times.)]|[if(%3,You have voted for [itemize(iter(%3,name(first(%i0,^)),|,|),|)] in the last 24 hours already.)],|),|,%b)]
&INC`VOTE [u(cobj,vote)]=@sql INSERT INTO `votes`(`voter`,`votee`,`scene_id`,`vote_date`) VALUES[iter(%1,{('%0','%i0','%2',NOW())},,\,)] ; @dolist/inline %1={@attach %!/inc`ap=%0,%i0,1}