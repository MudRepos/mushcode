&CMD`+INFO`PENNMUSH [u(cobj,info)]=$^(?s)(?\:\+)?(info|pinfo|bg|abi|ability|res|resource|npc|npcs|com|complication)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+INFO`MAIN
@set [u(cobj,info)]/CMD`+INFO`PENNMUSH=regexp
&CMD`+INFO`RHOSTMUSH [u(cobj,info)]=$^(?s)(?\:\+)?(info|pinfo|bg|abi|ability|res|resource|npc|npcs|com|complication)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+INFO`MAIN
@set [u(cobj,info)]/CMD`+INFO`RHOSTMUSH=regexp

&INIT`ABI [u(cobj,info)]=th u(setq,sysname,ABILITIES);th u(setq,root,101);th u(setq,private,0);th u(setq,comm,+abi);th u(setq,lockapprove,1)
&INIT`RES [u(cobj,info)]=th u(setq,sysname,RESOURCES);th u(setq,root,105);th u(setq,private,0);th u(setq,comm,+res);th u(setq,lockapprove,1)
&INIT`NPC [u(cobj,info)]=th u(setq,sysname,NPCS);th u(setq,root,107);th u(setq,private,0);th u(setq,comm,+npc);th u(setq,lockapprove,1)
&INIT`COM [u(cobj,info)]=th u(setq,sysname,COMPLICATIONS);th u(setq,root,109);th u(setq,private,0);th u(setq,comm,+com);th u(setq,lockapprove,1)

&INIT`ABILITY [u(cobj,info)]=@attach %!/INIT`ABI
&INIT`RESOURCE [u(cobj,info)]=@attach %!/INIT`RES
&INIT`NPCS [u(cobj,info)]=@attach %!/INIT`NPC
&INIT`COMPLICATION [u(cobj,info)]=@attach %!/INIT`COM

&CMD`+LIST`PENNMUSH [u(cobj,info)]=$^(?\:\+)?(list|sheet)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?$:@attach %!/CMD`+LIST`MAIN
@set [u(cobj,info)]/CMD`+LIST`PENNMUSH=regexp
&CMD`+LIST`RHOSTMUSH [u(cobj,info)]=$^(?s)(?\:\+)?(list|sheet)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+LIST`MAIN
@set [u(cobj,info)]/CMD`+LIST`RHOSTMUSH=regexp
&CMD`+LIST`MAIN [u(cobj,info)]=th u(setq,mode2,%1);@attach %!/INC`GETSWITCH=%2;@attach %!/%1`[u(strfirstof,%q<switch>,MAIN)]=%3,%4
@set [u(cobj,info)]/CMD`+LIST`[u(choosegame,RHOSTMUSH,PENNMUSH)]=no_command

&SWITCHES`PLAYER`EXTRA [u(cobj,info)]=switch(%q<mode2>,list,WIKIFY|ALL,GET|RENAME|DELETE|ALL)
&SWITCHES`ADMIN`EXTRA [u(cobj,info)]=switch(%q<mode2>,list,,LOCK|UNLOCK)

&FUN`ROOTID [u(cobj,info)]=switch(%0,ABILITIES,101,RESOURCES,105,NPCS,107,COMPLICATIONS,109)

&INC`ALL [u(cobj,info)]=@attach %!/INC`TARGET;@attach %!/LIST`MAIN=%q<t1>,%q<sysname>

&LIST`MAIN [u(cobj,info)]=@include %!/INC`CHECKPC=%0,1;th u(setq,target,%q<t1>);@include %!/INC`PARTIAL=%1,ABILITIES|RESOURCES|NPCS|COMPLICATIONS,|,choice,choice;@dolist/inline/delimit | [u(strfirstof,%q<choice>,ABILITIES|RESOURCES|NPCS|COMPLICATIONS)]={th u(setq,cdata1,u(choosegame,%i0,%d0));@pemit %#=u(HEADER,%q<t1name>'s [capstr(lcstr(%q<cdata1>))]);@dolist/inline/delimit [u(rsep)] [u(mysql3,LIST`POWERS,%q<t1id>,u(FUN`ROOTID,%q<cdata1>))]={th u(setq,cdata,u(choosegame,%i0,%d0));@pemit %#=u(separator,elements(%q<cdata>,2,u(fsep)) - [u(fancytime3,elements(%q<cdata>,3,u(fsep)),%#)]);@pemit %#=u(mysql,VIEW`FILE,elements(%q<cdata>,1,u(fsep)))}};@pemit %#=u(FOOTER)

&Q`LIST`POWERS [u(cobj,info)]=SELECT info_id,info_name,info_date_modified_secs FROM volv_infofile WHERE owner_id=? AND info_type=? ORDER BY info_name

&LIST`WIKIFY [u(cobj,info)]=@include %!/INC`CHECKPC=%0,1;th u(setq,target,%q<t1>);@include %!/INC`PARTIAL=%1,ABILITIES|RESOURCES|NPCS|COMPLICATIONS,|,choice,choice;@pemit %#=u(HEADER,WIKIFY: %q<t1name>);@dolist/inline/delimit | [u(strfirstof,%q<choice>,ABILITIES|RESOURCES|NPCS|COMPLICATIONS)]={@pemit %#=== [capstr(##)] ==;@dolist/inline/delimit [u(rsep)] [u(mysql3,LIST`POWERS,%q<t1id>,u(FUN`ROOTID,%i0))]={th u(setq,cdata,u(choosegame,%i0,%d0));@pemit %#='''[elements(%q<cdata>,2,u(fsep))]:''' [u(mysql,VIEW`FILE,elements(%q<cdata>,1,u(fsep)))]<br />}};@pemit %#=u(FOOTER)

&SHEET`MAIN [u(cobj,info)]=@include %!/INC`CHECKPC=u(strfirstof,%0,%#),1;th u(setq,target,%q<t1>);@include %!/INC`PARTIAL=%1,ABILITIES|RESOURCES|NPCS|COMPLICATIONS,|,choice,choice;@dolist/inline/delimit | [u(strfirstof,%q<choice>,ABILITIES|RESOURCES|NPCS|COMPLICATIONS)]={th u(setq,cdata,u(choosegame,%i0,%d0));@pemit %#=u(HEADER,%q<t1name>'s [capstr(lcstr(%q<cdata>))]);@pemit %#=u(setr,section,u(FUN`SHEETSECTION,%q<t1>,u(FUN`ROOTID,%q<cdata>),u(setr,com,switch(%q<cdata>,ABILITIES,+abi,RESOURCES,+res,NPCS,+npc,COMPLICATIONS,+fla))))[if(strlen(%q<section>),%R%R[center(%q<com> %q<t1name>/<entry>,u(setr,half,floor(fdiv(u(width,%#),2))))][center(%q<com>/all %q<t1name>,%q<half>)])]};@pemit %#=u(FOOTER,+sheet/all %q<t1name>)

&SHEET`ALL [u(cobj,info)]=@attach %!/LIST`MAIN=%0

&Q`LIST`POWER_NAMES [u(cobj,info)]=SELECT info_name FROM volv_infofile WHERE owner_id=? AND info_type=? ORDER BY info_name

&FUN`SHEETSECTION [u(cobj,info)]=table(iter(u(mysql3,LIST`POWER_NAMES,get(%0/D`ID),%1),center(u(pueblize,%i0,%2 [name(%0)]/%i0),23),u(rsep),u(rsep)),23,sub(u(width,%#),4),u(rsep))

@@ %R[center(%4 [name(%0)]/<entry> to view,sub(u(width,%#),4))])]

+help/add Character/+abilities=The Following commands use the same Syntax as +info (see +help +info) to handle data on your character. Note that none of these can be changed outside of Chargen except by admin.%R%R[ansi(h,+abi)] - Abilities.%R[ansi(h,+res)] - Resources.%R[ansi(h,+npc)] - NPCs.%R[ansi(h,+com)] - Complications.%R%RTo set one:%R+<command> <name>=<text>%Rexample:%R+abi Spider-Sense=It's tingling!
+help/category Powers=Character

@@ @dolist lsearch(all,type,player)={@dolist/inline CHARGEN`POWER~D`MAJORFILE CHARGEN`ADV~D`ADVFILE CHARGEN`FLAW~D`FLAWFILE={@dolist/inline lattr(%i1/[before(%i0,~)]`*)={th u(setq,attr,after(%i1,~)`[u(nextslot,%i2,after(%i1,~))]);&%q<attr> %i2=before(u(setr,contents,get(%i2/%i0)),|);&%q<attr>`CONTENTS %i2=after(%q<contents>,|);&%q<attr>`FLAGS %i2=SetBy~[u(objid,%i2)]|SetOn~[secs()]}}}

@@ @dolist lsearch(all,type,player)={@dolist/inline CHARGEN`BG~D`BGFILE={@dolist/inline lattr(%i1/[before(%i0,~)]*)={th u(setq,attr,after(%i1,~)`[u(nextslot,%i2,after(%i1,~))]);&%q<attr> %i2=Main;&%q<attr>`CONTENTS %i2=get(%i2/CHARGEN`BG);&%q<attr>`FLAGS %i2=SetBy~[u(objid,%i2)]|SetOn~[secs()]}}}
